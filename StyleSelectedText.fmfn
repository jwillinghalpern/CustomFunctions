/**
 * StyleSelectedValues ( fullList ; listToStyle ; style ; color )
 * 
 * PARAMS:
 * 	fullList: the value list containing all the possible values
 * 	listToStyle: The values you want to style. (Like the values selected by a checkbox)
 * 	style: a number useable by Filemaker's TextAddStyle() fn. E.g. 'bold + italic + underline'
 * 	color: a number useable by Filemaker's TextColor() fn. e.g. 'RGB( 123 ; 12 ; 16 )'
 * 	one: Always set to 1 or lower. Empty is ok too.
 * 
 * REVISIONS:
 * 	2025-12-06	Claude 4 Sonnet
 * 		- Fixed carriage return issue and converted from recursion to While() loop
 * 		- JWH: remove superfluous Let() wrappers
 * 	2018-11-26	Joshua Willing	jwillinghalpern@gmail.com
 * 		- added style and color params.
 * 		- No longer styles the carriage returns between values, just the values.
 * 	2018-11-22	Joshua Willing	jwillinghalpern@gmail.com
 * 		- CREATED
 */

While (
[ 
	~count = ValueCount ( listToStyle ) ;
	~i = 1 ;
	~currentList = fullList 
] ;
~i <= ~count ;
[
	~val = GetValue ( listToStyle ; ~i ) ;
	~styledVal = TextColor ( TextStyleAdd ( ~val ; style ) ; color ) ;
	~tempList = Substitute ( 
		¶ & ~currentList & ¶ ; 
		¶ & ~val & ¶ ; 
		¶ & ~styledVal & ¶ 
	) ;
	~currentList = Middle ( ~tempList ; 2 ; Length ( ~tempList ) - 2 ) ;
	~i = ~i + 1
] ;

~currentList

)